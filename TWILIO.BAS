1    'TWILIO.BA
2    'BARRETT OTTE 2020
3    'https://github.com/barrettotte/TRS80-Twilio
4    '
5    'A VERY SIMPLE PROGRAM TO SEND
6    'A TEXT MESSAGE USING TWILIO
7    'SMS API.
8    '
10   'SENDS BUFFER TO ESP8266 VIA RS232.
11   'ESP8266 WILL CALL TWILIO SMS API
12   'USING CONTENTS OF BUFFER
13   '
15   REM ******PROGRAM START*************
20   ON ERROR GOTO 4000: REM CATCHALL ERROR
25   CLEAR 1000: REM STRING SPACE IN MEMORY
30   OPEN "COM:38N1D" FOR OUTPUT AS 1: REM RS232 COMMS
50   LET O$="": REM OUTPUT BUFFER TO RS232
90   PRINT ""
100  PRINT "           TRS-80 TWILIO"
102  PRINT ""
105  PRINT "SEND A TEXT MESSAGE USING TWILIO SMS API"
110  PRINT ""
125  INPUT "PHONE NUMBER";P$
130  GOSUB 1000: REM ADDPHONE
135  O$=O$+";": REM SEP FOR BACKEND PARSING
140  INPUT "MESSAGE (128 MAX)";M$
145  IF LEN(M$)<=128 THEN GOTO 160
150  PRINT "MESSAGE IS TOO LONG. TRY AGAIN"
155  BEEP: BEEP: BEEP: GOTO 140
160  O$=O$+M$
170  REM PRINT O$: REM DEBUG
190  PRINT ""
195  PRINT "SENDING..."
200  PRINT "": BEEP: PRINT #1,O$
210  PRINT "SENT REQUEST OF" LEN(O$) "BYTE(S)"
220  PRINT "TO TWILIO SMS API"
230  GOTO 5000: REM END PGM
995  REM
1000 REM ******BEGIN SUBR ADDPHONE***
1001 REM   ADD P$ TO OUTPUT BUFFER
1003 REM   VERY BASIC VALIDATION
1010 FOR I=1 TO LEN(P$) STEP 1
1020 LET C$=MID$(P$,I,1)
1025 REM IF C$ IS A DIGIT [0..9] IN ASCII
1030 IF ASC(C$)>=48 AND ASC(C$)<=57 GOTO 1100
1035 REM ELSEIF OUTBUFF EMPTY AND C$= +
1040 IF C$="+" THEN 1045 ELSE 1060
1045 IF LEN(O$)=0 THEN 1100 ELSE 1050
1050 PRINT "UNEXPECTED '+' IN PHONE NUMBER"
1051 BEEP: BEEP: BEEP
1055 GOTO 110: REM RE-ENTER PHONE NUMBER
1060 REM ELSEIF CHAR CAN BE SKIPPED
1065 X$="()-. ": REM SKIPPABLE CHARS
1066 Y$=C$
1070 GOSUB 1395: REM CHFIND
1075 IF Z=1 GOTO 1200: REM SKIP
1080 REM ELSE CHAR IS INVALID
1085 PRINT "INVALID CHARACTER '" C$ "' IN PHONE NUMBER"
1086 BEEP: BEEP: BEEP
1090 GOTO 110: REM RE-ENTER PHONE NUMBER
1100 REM ADD CHAR TO OUTPUT BUFFER
1110 O$=O$+C$
1200 NEXT I
1210 IF LEN(O$)>6 AND LEN(O$)<17 GOTO 1250
1215 PRINT "PHONE NUMBER LENGTH INVALID"
1220 BEEP: BEEP: BEEP
1225 GOTO 110: REM RE-ENTER PHONE NUMBER
1250 RETURN
1255 REM ******END SUBR ADDPHONE*******
1390 REM
1395 REM ******BEGIN SUBR CHFIND*******
1400 REM Z$=(IF Y$ IN X$)?1:0
1405 Z=1
1410 FOR J=1 TO LEN(X$) STEP 1
1415 IF Y$=MID$(X$,J,1) THEN 1430
1420 NEXT J
1425 Z=0
1430 RETURN
1435 REM ******END SUBR CHFIND*********
3995 REM
4000 REM ******UNEXPECTED ERROR********
4005 BEEP: BEEP: BEEP
4010 PRINT "UNEXPECTED ERROR ENCOUNTERED :("
4020 PRINT "  LINE: " ERL
4025 PRINT "  CODE: " ((ERR/2)+1)
4030 GOTO 5000
4095 REM
5000 REM ******PROGRAM END*************
5010 CLOSE 1: REM CLOSE RS232 COMMS
5050 END